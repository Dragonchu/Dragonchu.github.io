---
title: "NodeMCU远程点亮电灯泡"
date: 2022-04-21
draft: false
tags: ["归档","嵌入式开发"]
description: "快速入门NodeMCU，开发板的Hello Worlds"
---

# **NodeMCU远程点亮灯泡**

**目标：**远程点亮一个灯泡

**硬件**：ModeMCU

![Untitled](NodeMCU%E8%BF%9C%E7%A8%8B%E7%82%B9%E4%BA%AE%E7%94%B5%E7%81%AF%E6%B3%A1%208fbf8dba49e944708898e9ff11403d40/Untitled.png)

## **基础知识**

**ESP8266和NodeMCU：**

ESP8266是一块芯片，NodeMCU是以ESP8266芯片为核心的开发板

![esp8266芯片](NodeMCU%E8%BF%9C%E7%A8%8B%E7%82%B9%E4%BA%AE%E7%94%B5%E7%81%AF%E6%B3%A1%208fbf8dba49e944708898e9ff11403d40/Untitled.jpeg)

esp8266芯片

![内置ESP8266芯片的nodemcu开发板](NodeMCU%E8%BF%9C%E7%A8%8B%E7%82%B9%E4%BA%AE%E7%94%B5%E7%81%AF%E6%B3%A1%208fbf8dba49e944708898e9ff11403d40/Untitled%201.jpeg)

内置ESP8266芯片的nodemcu开发板

**ESP8266的三种工作模式：**

- STA(Station)：ESP8266主动去连接WIFI
- AP(Wireless Access Point)：ESP8266创建WIFI
- STA+AP：上面两个共存

**API：**

[WiFiServer](https://www.arduino.cc/reference/en/libraries/wifi/wifiserver/)，[Serial.begin()](https://www.arduino.cc/reference/en/language/functions/communication/serial/begin/)，[pinMode()](https://www.arduino.cc/reference/en/language/functions/digital-io/pinmode/)，[Serial.print()](https://www.arduino.cc/reference/en/language/functions/communication/serial/print/)

**板载小灯：**

GPIO2引脚与板载小灯相联

在esp8266的库文件中hardware/esp8266/3.0.2/variants/nodemcu/pins_arduino.h中定义了各种引脚

![Untitled](NodeMCU%E8%BF%9C%E7%A8%8B%E7%82%B9%E4%BA%AE%E7%94%B5%E7%81%AF%E6%B3%A1%208fbf8dba49e944708898e9ff11403d40/Untitled%201.png)

## **基本思路**

ESP8266以STA模式工作，连接到WIFI后，自己内部启动一个TCP server，这个TCP server其实是写一个小型的HTTP服务器，客户端通过浏览器就可以访问到。

**代码：**

```cpp
#include <ESP8266WiFi.h>

//此处设置您的wifi接入信息
#ifndef STASSID
#define STASSID "WiFi" // wifi名称
#define STAPSK "password" //要连接的wifi密码
#endif

const char *ssid = STASSID;
const char *password = STAPSK;

//创建TCP server对象,并将端口指定为80
WiFiServer server(80);

void setup()
{
  //打开串口
  Serial.begin(115200);

  //初始化板载LED
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, 0);

  //连接WiFi
  Serial.println();
  Serial.println();
  Serial.print(F("Connecting to "));
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(F("."));
  }
  Serial.println();
  Serial.println(F("WiFi connected"));

  //启动TCP server服务器
  server.begin();
  Serial.println(F("Server started"));

  //打印IP地址
  Serial.println(WiFi.localIP());
}

void loop()
{
  //检查客户端是否已连接
  WiFiClient client = server.available();
  if (!client)
  {
    return;
  }
  Serial.println(F("new client"));

  client.setTimeout(5000); //默认为 1000

  //读取客户端发起的TCP请求
  String req = client.readStringUntil('\r');
  Serial.println(F("request: "));
  Serial.println(req);

  //解析请求里是否含有以下指定内容
  int val;
  //检查发起的请求内容是否包含"/gpio/0"
  if (req.indexOf(F("/gpio/0")) != -1)
  {
    val = 0;

    //检查发起的请求内容是否包含"/gpio/1"
  }
  else if (req.indexOf(F("/gpio/1")) != -1)
  {
    val = 1;
  }
  else
  {
    Serial.println(F("invalid request"));
    val = digitalRead(LED_BUILTIN);
  }

  //根据要求设置板载LED状态
  digitalWrite(LED_BUILTIN, val);

  //读取剩余的内容,用于清除缓存
  while (client.available())
  {
    client.read();
  }

  //将响应体发送到客户端 注:若要使用中文则必须在其中添加<meta charset=\"utf-8\">声明编码
  client.print(F("HTTP/1.1 200 OK\r\nContent-Type: text/html\r\n\r\n<!DOCTYPE HTML>\r\n<html>\r\n<meta charset=\"utf-8\">NodeMCU板载LED灯的状态 : "));
  client.print((val) ? F("关") : F("开"));
  client.print(F("<br><br> <a href='http://"));
  client.print(WiFi.localIP());
  client.print(F("/gpio/1'>  点击关灯  </a> , 或  <a href='http://"));
  client.print(WiFi.localIP());
  client.print(F("/gpio/0'>  点击开灯  </a> </html>"));

  //通过浏览器点击按钮之后,TCP连接将会被重置,并且浏览器会发起新的TCP请求到我们的8266服务器,从而去控制LED灯
  Serial.println(F("Disconnecting from client"));
}
```

## **参考资料：**

[太极创客](http://www.taichi-maker.com/homepage/esp8266-nodemcu-iot/esp8266-nodemcu-tutorial-index/nodemcu-board/)